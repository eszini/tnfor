!     ******************************************************************
!     ECONINTR.FOR
!     Copyright(c)  2000
!
!     Created: 11/15/2006 1:56:02 PM
!     Author : MARK S GERBER
!     Last change: 
!			MSG 11/5/2015 3:25:04 PM
!     ******************************************************************

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
      SUBROUTINE NEW_ECONOMY_INTERCHANGE(RR_AT_LOAD_POINT, &
        OBS_AT_LOAD_POINT, &
        NUMBER_OF_SEGMENTS,ENERGY,EAVAIL,BLKNO, &
        ANNUAL_ECONOMY_BOUGHT, &
        ANNUAL_ECONOMY_SOLD, &
        ANNUAL_ECONOMY_COST,ANNUAL_ECONOMY_REVENUE, &
        UNIT,SEAS_HOURS, &
        LEFT,RIGHT,MAINTENANCE_RATE,YR,LEFT_HEAT,RIGHT_HEAT, &
        IMPORT_CAP,EXPORT_CAP, &
        PERIOD_COUNTER, &
        TRANSACTION_BUY_SPREAD,TRANSACTION_SELL_SPREAD, &
        MONTHLY_ECONOMY_BOUGHT,MONTHLY_ECONOMY_COST, &
        MONTHLY_ECONOMY_SOLD,MONTHLY_ECONOMY_REVENUE, &
        ECONOMY_TRANS_TYPE, &
        CL_POOL_FRAC_OWN,MWBLOK,LODDUR,NUNITS,SRP_SEGMENT_COST,TEMPEA, &
        GENGRP) 
      use end_routine, only: end_program, er_message
      use grx_planning_routines
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
      use spindriftlib
      use prod_arrays_dimensions
      USE IREC_ENDPOINT_CONTROL
      use eco
      use globecom
      USE SIZECOM
      use poolcom

!
      LOGICAL (KIND=1) :: SELL_AGAINST_UNIT,POOLING_TRANSACTIONS
      CHARACTER (LEN=1) :: ECONOMY_TRANS_TYPE(MAX_CL_UNITS)
      REAL :: UNIT_POWER
!
      REAL :: TRANSACTION_BUY_SPREAD,TRANSACTION_SELL_SPREAD
      REAL :: SEGMENT_ENERGY_UNUSED,SEGMENT_GENERATION,SEGMENT_ENERGY_BOUGHT(MAX_DISPATCH_BLOCKS), &
            SEGMENT_ENERGY_SOLD(MAX_DISPATCH_BLOCKS),RR_ENERGY_TO_BUY,RR_ENERGY_TO_SELL, &
            ENERGY(2,MAX_CL_UNITS),EAVAIL(MAX_CL_UNITS),SEAS_HOURS,MWBLOK(MAX_DISPATCH_BLOCKS),ANNUAL_ECONOMY_BOUGHT, &
            ANNUAL_ECONOMY_SOLD,MONTHLY_ECONOMY_BOUGHT,MONTHLY_ECONOMY_SOLD,MONTHLY_ECONOMY_COST,MONTHLY_ECONOMY_REVENUE, &
            ANNUAL_ECONOMY_COST,ANNUAL_ECONOMY_REVENUE,LEFT(MAX_DISPATCH_BLOCKS),RIGHT(MAX_DISPATCH_BLOCKS),RIGHT_RIGHT,RR_PROB, &
            TRANSACT_PROB,TIE_CAP,MAINTENANCE_RATE(MAX_CL_UNITS),LAST_RIGHT,TOP_PROB,BOTTOM_PROB,LAST_PROB,SUM_PROB,TRANS_COST, &
            LEFT_HEAT(MAX_CL_UNITS),RIGHT_HEAT(MAX_CL_UNITS),IMPORT_CAP,EXPORT_CAP,RR_COST,CL_POOL_FRAC_OWN(MAX_CL_UNITS), &
            TEMP_BUY_ENERGY,TEMP_SELL_ENERGY,EC_UTIL_PORTION(MAX_DISPATCH_BLOCKS) 
      REAL :: EC_POOL_PORTION(MAX_DISPATCH_BLOCKS),SEGMENT_ENERGY_COST(MAX_DISPATCH_BLOCKS), &
            SEGMENT_ENERGY_REV(MAX_DISPATCH_BLOCKS),SEGMENT_BUYS(MAX_DISPATCH_BLOCKS),SEGMENT_SELLS(MAX_DISPATCH_BLOCKS), &
            L_TEMP_HEAT(0:MAX_DISPATCH_BLOCKS),R_TEMP_HEAT(0:MAX_DISPATCH_BLOCKS),BASE_MW,FIRST_SEGMENT_MW_ABOVE_BASE, &
            LODDUR(CONVOLUTION_POINTS),TIE_PERCENT,SELL_PRICE,BUY_PRICE,OBS_AT_LOAD_POINT(100),RR_AT_LOAD_POINT(100)
!    
      CHARACTER (LEN=2) :: EXT(16)
      CHARACTER (LEN=12) :: FILE_NAME
      DATA EXT /'96','97','98','99','00','01','02','03','04','05','06','07','08','09','10','11'/
      LOGICAL (KIND=1) :: EXTERNAL_RR
      INTEGER (KIND=2) :: EXTERNAL_ECONOMY_INTER_NO=0
      INTEGER (KIND=2) :: GET_EXT_ECONOMY_INTER_NO
      LOGICAL (KIND=4) :: FILE_EXISTS
      REAL (KIND=4) :: EXT_RR_AT_LOAD_POINT(:)
      ALLOCATABLE :: EXT_RR_AT_LOAD_POINT
!
      INTEGER (KIND=2) :: I,U,NUMBER_OF_LOAD_POINTS,NUMBER_OF_SEGMENTS,BLKNO(MAX_DISPATCH_BLOCKS), &
               ECBLK,UNIT(MAX_DISPATCH_BLOCKS),K,BASE_K,NEXT_K,ICOST,KCOST,YR,S_MAX,ILAST,PERIOD_COUNTER,LAST_BUY_SEGMENT, &
               LAST_SELL_SEGMENT,IPNT,LOAD_POINT
      LOGICAL (KIND=1) :: FIRST_UNIT_PASS,INCREMENT_KCOST,SPLIT_SAVINGS,ECON_PRICING
      INTEGER (KIND=4) :: S
      REAL :: EXTENSION_MULT
! ECONOMY REPORT VARIABLES
      LOGICAL (KIND=1) :: ECONOMY_INTER_REPORT_NOT_OPEN=.TRUE.
      LOGICAL (KIND=1) :: ECONOMY_REPORT,ECONOMY_REPORT_ACTIVE
      INTEGER (KIND=2) :: ECONOMY_INTER_NO,ECONOMY_INTER_HEADER
      INTEGER (KIND=2) :: LAST_SEASON,PRODUCTION_PERIODS,TRANSACTION_POINT,LAST_TRANSACTION,GENGRP(MAX_CL_UNITS),GRP
      REAL :: TRANSACTION_ENERGY_BOUGHT,TRANSACTION_COST,TRANSACTION_ENERGY_SOLD,TRANSACTION_REVENUE,TRANSACTION_AVE_BUY, &
              TRANSACTION_AVE_SELL,SRP_SEGMENT_COST(*),PERIOD_NON_DISPATCHING_REV,ANNUAL_NON_DISPATCHING_REV=0., &
              R_SRP_NON_DISPATCH_REV,TEMPEA(2,MAX_CL_UNITS),GROUP_ECO_SALES(0:MAX_REPORTING_GROUPS), &
              GROUP_ECO_PURCHASES(0:MAX_REPORTING_GROUPS),R_GROUP_ECO_SALES(MAX_REPORTING_GROUPS), &
              R_GROUP_ECO_PURCHASES(MAX_REPORTING_GROUPS)
      CHARACTER (LEN=9) :: CL_MONTH_NAME(13),MONTH_NAME*20
      INTEGER :: ECONOMY_INTER_REC
      SAVE ECONOMY_INTER_REC
      SAVE  ECONOMY_INTER_NO,LAST_SEASON,CL_MONTH_NAME,GROUP_ECO_SALES,GROUP_ECO_PURCHASES
!
      REAL :: BLOCK_DISP_COST(:,:)
      ALLOCATABLE :: BLOCK_DISP_COST
      INTEGER (KIND=2) :: KCOST_UNIT,KCOST_BLKNO,LAST_TRANS_UNIT
      INTEGER (KIND=2) :: LAST_TRANS_BLKNO,NUNITS
!      
! END OF DATA DECLARATIONS
!
      ALLOCATE(BLOCK_DISP_COST(NUNITS,2)) ! DEALLOCATED AT BOTTOM
      CALL GET_BLOCK_DISP_COST(UNIT,BLKNO,BLOCK_DISP_COST,NUMBER_OF_SEGMENTS)
!      
!
      IF(PERIOD_COUNTER == 1) THEN
         ANNUAL_NON_DISPATCHING_REV = 0.
         GROUP_ECO_SALES = 0.
         GROUP_ECO_PURCHASES = 0.
      ENDIF
      PERIOD_NON_DISPATCHING_REV = 0.
!
!      
      ECONOMY_REPORT_ACTIVE = ECONOMY_REPORT()
      SPLIT_SAVINGS = ECON_PRICING()
      IF(ECONOMY_REPORT_ACTIVE .AND. .NOT. TESTING_PLAN) THEN
         IF(ECONOMY_INTER_REPORT_NOT_OPEN) THEN
            ECONOMY_INTER_NO = ECONOMY_INTER_HEADER(ECONOMY_INTER_REC)
            ECONOMY_INTER_REPORT_NOT_OPEN = .FALSE.
            LAST_SEASON = PRODUCTION_PERIODS()
            DO I = 1, LAST_SEASON
               CL_MONTH_NAME(I) = MONTH_NAME(I)
            ENDDO
            CL_MONTH_NAME(LAST_SEASON+1) = 'Annual'
         ENDIF
      ENDIF
!
! END EFFECTS INITIALIZATION
!
      EXTENSION_MULT = 1.
      IF(YEAR > STUDY_PERIOD) THEN
         IF(YEAR == STUDY_PERIOD + 1 .AND. PERIOD_COUNTER == 1) THEN
            CALL CALC_ECONOMY_EXTENSION_MULT(YEAR)
         ENDIF
         CALL GET_ECONOMY_EXTENSION_MULT(PERIOD_COUNTER,EXTENSION_MULT)
      ENDIF
!
!     INITIALIZE VARIABLES
!
      EXTERNAL_RR = .FALSE.
      IF(EXTERNAL_RR) THEN
         NUMBER_OF_LOAD_POINTS = SEAS_HOURS
         IF(SEAS_HOURS > 744.) THEN
            WRITE(4,*) "*** STOP at line 159 NEW_ECONOMY_INTERCHANGE"
            WRITE(4,*) "MUST RUN MONTHLY PRODUCTION IF USING "
            WRITE(4,*) "EXTERNAL SOURCE OF RUNNING RATES"
            er_message='See WARNING MESSAGES -ECONINTR.FOR-1'
            call end_program(er_message)
         ENDIF
         ALLOCATE(EXT_RR_AT_LOAD_POINT(800))
         IF(PERIOD_COUNTER == 1) THEN
            EXTERNAL_ECONOMY_INTER_NO = GET_EXT_ECONOMY_INTER_NO()
            FILE_NAME = 'BASE_PR.R'//EXT(YR) ! NEED TO MAKE THE NAME GENERAL
            INQUIRE(FILE=FILE_NAME,EXIST=FILE_EXISTS)
            IF(FILE_EXISTS) THEN
               OPEN(EXTERNAL_ECONOMY_INTER_NO,FILE= FILE_NAME,ACCESS="DIRECT",RECL=3200)
            ELSE
               WRITE(4,*) "*** STOP at line 173 NEW_ECONOMY_INTERCHANGE"
               WRITE(4,*) "EXTERNAL SOURCE FILE",FILE_NAME
               WRITE(4,*) "DOES NOT EXIST"
               er_message='See WARNING MESSAGES -ECONINTR.FOR-2'
               call end_program(er_message)
            ENDIF
         ENDIF
         READ(EXTERNAL_ECONOMY_INTER_NO,REC=PERIOD_COUNTER) EXT_RR_AT_LOAD_POINT
      ELSE
         NUMBER_OF_LOAD_POINTS = IPNT()
      ENDIF
      MONTHLY_ECONOMY_BOUGHT = 0.
      MONTHLY_ECONOMY_COST = 0.
      MONTHLY_ECONOMY_SOLD = 0.
      MONTHLY_ECONOMY_REVENUE = 0.
      SUM_PROB = 0.
      L_TEMP_HEAT(0) = 0.0
      R_TEMP_HEAT(0) = 0.0
      DO S = 1 , MAX_DISPATCH_BLOCKS
         SEGMENT_ENERGY_BOUGHT(S) = 0.
         SEGMENT_ENERGY_SOLD(S) = 0.
         SEGMENT_ENERGY_COST(S) = 0.
         SEGMENT_ENERGY_REV(S) = 0.
         SEGMENT_BUYS(S) = 0.
         SEGMENT_SELLS(S) = 0.
         L_TEMP_HEAT(S) = LEFT(S)
         R_TEMP_HEAT(S) = RIGHT(S)
      ENDDO
      IF(POOLING_TRANSACTIONS()) THEN
         DO S = 1 , MAX_CL_UNITS
            EC_UTIL_PORTION(S) = CL_POOL_FRAC_OWN(S) / 100.
            EC_POOL_PORTION(S) = 1. - EC_UTIL_PORTION(S)
         ENDDO
      ENDIF
!
!     FIND THE UNIT ASSOCIATED WITH MINIMUM LOAD
!
      RR_PROB = 1.
      BASE_K = 1
      BASE_MW = 0.
      DO WHILE(RIGHT(BASE_K) >= 1. .AND. BASE_K .LT. NUMBER_OF_SEGMENTS)
         ECBLK = BLKNO(BASE_K)
         U = UNIT(BASE_K)
         BASE_MW  = BASE_MW + MWBLOK(BASE_K) * (1. - MAINTENANCE_RATE(U)) ! FOR'S ARE CONVOLVED
         BASE_K = BASE_K+1
      ENDDO
!      
      LOAD_POINT = 1
      IF(BASE_MW + MWBLOK(BASE_K) < LODDUR(LOAD_POINT)) THEN
         WRITE(4,*) "*** STOP at line 223 NEW_ECONOMY_INTERCHANGE"
         WRITE(4,*) "First economy interchange unit not found"
         er_message='See WARNING MESSAGES -ECONINTR.FOR-3'
         call end_program(er_message)
      ELSE
         DO WHILE(BASE_MW > LODDUR(LOAD_POINT) ) 
            LOAD_POINT = LOAD_POINT + 1
            IF(LOAD_POINT > NUMBER_OF_LOAD_POINTS) THEN
               WRITE(4,*) "*** STOP at line 230 NEW_ECONOMY_INTERCHANGE"
               WRITE(4,*) "First economy interchange unit not found"
               er_message='See WARNING MESSAGES -ECONINTR.FOR-4'
               call end_program(er_message)
            ENDIF
         ENDDO
         FIRST_SEGMENT_MW_ABOVE_BASE = MWBLOK(BASE_K) - (LODDUR(LOAD_POINT) - BASE_MW)
      ENDIF
!      
      K = BASE_K
      S = K
      NEXT_K = K
      DO WHILE(RIGHT(NEXT_K) .GE. RIGHT(K) .AND. RIGHT(K) .NE. 0.)
         NEXT_K = NEXT_K + 1
      ENDDO
      ILAST = 0
      LAST_RIGHT = RR_PROB
      BOTTOM_PROB = RR_PROB
      RIGHT_RIGHT = LAST_RIGHT - RIGHT(K)
      FIRST_UNIT_PASS = .TRUE.
!
!     I RUNS THROUGH THE RR'S. K RUNS THROUGH THE MARGINAL SEGMENTS,
!     S RUNS THROUGH THE TRANSACTION SEGMENTS.
!
         I = 1
         ICOST = 1
         KCOST = BASE_K
         LAST_PROB = 1.
         IF(EXTERNAL_RR) THEN
            RR_PROB = 1. - FLOAT(I+1)/SEAS_HOURS
         ELSE
            RR_PROB = OBS_AT_LOAD_POINT(I+1)
         ENDIF
         S_MAX = 0
         LAST_BUY_SEGMENT = 0
         LAST_SELL_SEGMENT = 0
!
         TRANSACTION_POINT = 1         
!
!        TAKE INTO ACCOUNT THE CASE WHERE MULTIPLE UNITS CAN
!        CROSS A SINGLE RR SEGMENT
!
         DO WHILE(KCOST < NUMBER_OF_SEGMENTS .AND. ICOST < NUMBER_OF_LOAD_POINTS)
!     
            TOP_PROB = BOTTOM_PROB
!
!           THE PREVIOUS HIGH VALUE BECOMES THE NEW HIGH VALUE
!           AND THE NEXT HIGHEST VALUE BECOMES THE NEW BOTTOM VALUE.
!           THE TOP AND BOTTOM DEFINE THE TRANSACTION INTERVAL.
!
            IF(TOP_PROB .EQ. LAST_PROB .AND. TOP_PROB .EQ. LAST_RIGHT) THEN
               BOTTOM_PROB = MAX(RR_PROB,RIGHT(K))
            ELSEIF(TOP_PROB .EQ. LAST_PROB) THEN
               BOTTOM_PROB = MAX(RR_PROB,LAST_RIGHT)
            ELSEIF(TOP_PROB .EQ. LAST_RIGHT) THEN   
               BOTTOM_PROB = MAX(RIGHT(K),LAST_PROB)
            ENDIF
            TRANSACT_PROB = TOP_PROB - BOTTOM_PROB
            SUM_PROB = SUM_PROB + TRANSACT_PROB
            IF(EXTERNAL_RR) THEN
               RR_COST = EXT_RR_AT_LOAD_POINT(ICOST)*10.*EXTENSION_MULT
            ELSE
               RR_COST = RR_AT_LOAD_POINT(ICOST) * EXTENSION_MULT
            ENDIF
            TRANSACTION_ENERGY_BOUGHT = 0.
            TRANSACTION_COST = 0.
            TRANSACTION_ENERGY_SOLD = 0.
            TRANSACTION_REVENUE = 0.
            ECBLK = MAX(int(1,2),BLKNO(S))
            U = UNIT(S)
!
! UNIT SPECIFIC VALUES MOVED 10/7/92
!
            TIE_CAP = 0.
!
! ADDED 10/7/92 FOR TIM BG&E MSG
!
            IF(RR_COST .LT. BLOCK_DISP_COST(U,ECBLK)) THEN
               TRANS_COST = RR_COST * (1. + TRANSACTION_BUY_SPREAD)
!
!              CHECK FOR ABILITY TO BUY ENERGY
!
               DO WHILE (TRANS_COST < BLOCK_DISP_COST(U,ECBLK) .AND. TIE_CAP < IMPORT_CAP .AND. &
                                BLKNO(S) > 0 .AND. S >LAST_BUY_SEGMENT)
!
!                  U = UNIT(S) 
                  IF((ECONOMY_TRANS_TYPE(U) == 'B') .OR. (ECONOMY_TRANS_TYPE(U) == 'T')) THEN
                     UNIT_POWER = MIN(IMPORT_CAP - TIE_CAP,MWBLOK(S))
                     TIE_CAP = TIE_CAP + UNIT_POWER
!
!                 BUY UNTIL THE RR PRICE IS GREATER THAN SEGMENT_COST 
!
                     SEGMENT_GENERATION=ENERGY(ECBLK,U)*TEMPEA(ECBLK,U) ! 6/17/96. GAT.
                     IF(FIRST_UNIT_PASS .AND. UNIT_POWER >= MWBLOK(S)) THEN
                        IF(TOP_PROB == 1.0) THEN
                           RR_ENERGY_TO_BUY = TRANSACT_PROB * (MWBLOK(S) - FIRST_SEGMENT_MW_ABOVE_BASE)* &
                          (1. - MAINTENANCE_RATE(U)) * EAVAIL(U)* (TRANSACT_PROB*.5 + MAX(LEFT(K)-TOP_PROB,0.))/RIGHT_RIGHT
                        ELSEIF(RIGHT_RIGHT > 0.000001) THEN
                           RR_ENERGY_TO_BUY = TRANSACT_PROB * MWBLOK(S) * (1. - MAINTENANCE_RATE(U)) * &
                           EAVAIL(U)* (TRANSACT_PROB*.5 + MAX(LEFT(K)-TOP_PROB,0.))/RIGHT_RIGHT
                        ELSE
                           RR_ENERGY_TO_BUY = 0.
                        ENDIF
                     ELSE
                        RR_ENERGY_TO_BUY = TRANSACT_PROB * UNIT_POWER * (1. - MAINTENANCE_RATE(U)) *EAVAIL(U)
                     ENDIF
! MODIFIED: 10/19/92 FOR POOLING TRANSACTIONS                     
                     IF(RR_ENERGY_TO_BUY .LT. SEGMENT_GENERATION) THEN
                        TEMP_BUY_ENERGY = RR_ENERGY_TO_BUY
                        ENERGY(ECBLK,U) = ENERGY(ECBLK,U) - TEMP_BUY_ENERGY/TEMPEA(ECBLK,U) ! 6/17/96. GAT.
                     ELSEIF(SEGMENT_GENERATION .GT. 0.) THEN
                        TEMP_BUY_ENERGY = SEGMENT_GENERATION
                        ENERGY(ECBLK,U) = 0.
                     ELSE
                        TEMP_BUY_ENERGY = 0.
                     ENDIF
                     SEGMENT_ENERGY_BOUGHT(S) = TEMP_BUY_ENERGY + SEGMENT_ENERGY_BOUGHT(S)

                     TRANSACTION_ENERGY_BOUGHT = TRANSACTION_ENERGY_BOUGHT + TEMP_BUY_ENERGY
                     IF(SPLIT_SAVINGS) THEN
                        BUY_PRICE =(BLOCK_DISP_COST(U,ECBLK)+RR_COST)/2. 
                     ELSE
                        BUY_PRICE = RR_COST
                     ENDIF
                     IF(POOLING_TRANSACTIONS()) THEN
!                     
! NOTE 10/19/92: TRANSACTIONS VARYING BY AVERAGE NOT MARGINAL COST                     
!
                        IF(EC_POOL_PORTION(U) .GT. 0.) THEN
                           P_CLASS_ASS_ECON_BUY(2) = P_CLASS_ASS_ECON_BUY(2) + TEMP_BUY_ENERGY*SEAS_HOURS * EC_POOL_PORTION(U)
                           P_CLASS_ASS_ECON_COST(2) = P_CLASS_ASS_ECON_COST(2) + TEMP_BUY_ENERGY*SEAS_HOURS * &
                           EC_POOL_PORTION(U)*BUY_PRICE/10.
                           TEMP_BUY_ENERGY = TEMP_BUY_ENERGY * EC_UTIL_PORTION(U)
                        ENDIF
                        P_CLASS_ASS_ECON_BUY(1) = P_CLASS_ASS_ECON_BUY(1) + TEMP_BUY_ENERGY*SEAS_HOURS
                        P_CLASS_ASS_ECON_COST(1) = P_CLASS_ASS_ECON_COST(1) +TEMP_BUY_ENERGY*SEAS_HOURS*BUY_PRICE/10.
                     ENDIF ! END BUY POOLING SECTION
! TRAP FOR POOLING 

                     TRANSACTION_COST = TRANSACTION_COST + TEMP_BUY_ENERGY*BUY_PRICE
!     CHECKING THE IMPACT FOR BG&E OF EXTERNAL RUNNING COSTS
!                     MONTHLY_ECONOMY_COST = MONTHLY_ECONOMY_COST +
!     +                               TEMP_BUY_ENERGY*RR_COST
                     SEGMENT_ENERGY_COST(S) = SEGMENT_ENERGY_COST(S) + TEMP_BUY_ENERGY*BUY_PRICE
                     SEGMENT_BUYS(S) = SEGMENT_BUYS(S) + 1.
!
!                 BUY HEAT RATE CALCULATIONS
!                                         
! MODIFIED HEAT CALCULATION 10/30/92 MSG
                     R_TEMP_HEAT(S) = MIN(BOTTOM_PROB,R_TEMP_HEAT(S))
                     L_TEMP_HEAT(S) = BOTTOM_PROB

                  ENDIF ! BUY AGAINST UNIT
                  S = S - 1
                  IF(S < 1) EXIT
                  U = UNIT(S) ! MOVED FROM ABOVE. 2/19/96. GAT.
                  ECBLK = MAX(int(1,2),BLKNO(S)) ! MOVED FROM ABOVE. 2/19/96. GAT.
                  FIRST_UNIT_PASS = .FALSE.
               ENDDO
               LAST_BUY_SEGMENT = 0
            ELSEIF(RR_COST .GT. BLOCK_DISP_COST(U,ECBLK)) THEN
   40          U = UNIT(S)
               ECBLK = MAX(BLKNO(S),int(1,2))
               SELL_AGAINST_UNIT = (ECONOMY_TRANS_TYPE(U) == 'S') .OR. (ECONOMY_TRANS_TYPE(U) == 'T')
               TRANS_COST = BLOCK_DISP_COST(U,ECBLK)*(1.+TRANSACTION_SELL_SPREAD)
               IF(RR_COST > TRANS_COST .AND. TIE_CAP < EXPORT_CAP) THEN
                  IF( .NOT. SELL_AGAINST_UNIT) GOTO 50
                  UNIT_POWER = MIN(EXPORT_CAP - TIE_CAP, MWBLOK(S))
                  TIE_CAP = TIE_CAP + UNIT_POWER
                  IF(FIRST_UNIT_PASS .AND. UNIT_POWER >= MWBLOK(S)) THEN
                     IF(TOP_PROB == 1.0) THEN
                        RR_ENERGY_TO_SELL = TRANSACT_PROB * FIRST_SEGMENT_MW_ABOVE_BASE * (1. - MAINTENANCE_RATE(U)) * EAVAIL(U)* &
                          (TRANSACT_PROB*.5 + MAX(BOTTOM_PROB-RIGHT(K),0.))/RIGHT_RIGHT

                     ELSEIF(RIGHT_RIGHT > 0.) THEN
                        RR_ENERGY_TO_SELL = TRANSACT_PROB * MWBLOK(S) * (1. - MAINTENANCE_RATE(U)) * &
                        EAVAIL(U)* (TRANSACT_PROB*.5 + MAX(BOTTOM_PROB-RIGHT(K),0.))/RIGHT_RIGHT
                     ELSE
                        RR_ENERGY_TO_SELL = 0.
                     ENDIF
                     FIRST_UNIT_PASS = .FALSE.
                  ELSE
                     RR_ENERGY_TO_SELL = TRANSACT_PROB * UNIT_POWER * (1. - MAINTENANCE_RATE(U)) * EAVAIL(U)
                  ENDIF
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
                  SEGMENT_ENERGY_UNUSED = (MWBLOK(S) * (1. - MAINTENANCE_RATE(U)) - ENERGY(ECBLK,U)) * EAVAIL(U)
                  IF(RR_ENERGY_TO_SELL .LT. SEGMENT_ENERGY_UNUSED) THEN
                     TEMP_SELL_ENERGY = RR_ENERGY_TO_SELL
                     ENERGY(ECBLK,U) = ENERGY(ECBLK,U) + RR_ENERGY_TO_SELL / EAVAIL(U)
                  ELSEIF(SEGMENT_ENERGY_UNUSED .GT. 0.) THEN
                     TEMP_SELL_ENERGY = SEGMENT_ENERGY_UNUSED
                     ENERGY(ECBLK,U) = MWBLOK(S) * (1. - MAINTENANCE_RATE(U))
                  ELSE
                     TEMP_SELL_ENERGY = 0.
                  ENDIF
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  SEGMENT_ENERGY_UNUSED = 
!     +               MWBLOK(S) * 
!     +                  (1. - MAINTENANCE_RATE(U)) * EAVAIL(U) -
!     +                                 ENERGY(ECBLK,U) * TEMPEA(ECBLK,U)
!                  IF(RR_ENERGY_TO_SELL .LT. SEGMENT_ENERGY_UNUSED) THEN
!                     TEMP_SELL_ENERGY = RR_ENERGY_TO_SELL
!                     ENERGY(ECBLK,U) = ENERGY(ECBLK,U) +
!     +                               RR_ENERGY_TO_SELL / TEMPEA(ECBLK,U)
!                  ELSEIF(SEGMENT_ENERGY_UNUSED .GT. 0.) THEN
!                     TEMP_SELL_ENERGY = SEGMENT_ENERGY_UNUSED
!                     IF(TEMPEA(ECBLK,U) == EAVAIL(U)) THEN
!                        ENERGY(ECBLK,U) =  
!     +                            MWBLOK(S) * (1. - MAINTENANCE_RATE(U))
!                     ELSE
!                        ENERGY(ECBLK,U) =  EAVAIL(U) *
!     +                           MWBLOK(S) * (1. - MAINTENANCE_RATE(U))
!                     ENDIF
!                  ELSE
!                     TEMP_SELL_ENERGY = 0.
!                  ENDIF
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
                  SEGMENT_ENERGY_SOLD(S) = SEGMENT_ENERGY_SOLD(S) + TEMP_SELL_ENERGY

                  TRANSACTION_ENERGY_SOLD = TRANSACTION_ENERGY_SOLD + TEMP_SELL_ENERGY
                  IF(SPLIT_SAVINGS) THEN
                     SELL_PRICE =(BLOCK_DISP_COST(U,ECBLK)+RR_COST)/2. 
                  ELSE
                     SELL_PRICE = RR_COST
                  ENDIF
                  IF(POOLING_TRANSACTIONS()) THEN
!                     
! NOTE 10/19/92: TRANSACTIONS VARYING BY AVERAGE NOT MARGINAL COST                     
!
                     IF(EC_POOL_PORTION(U) .GT. 0.) THEN
                        P_CLASS_ASS_ECON_SELL(2) = P_CLASS_ASS_ECON_SELL(2) + TEMP_SELL_ENERGY * SEAS_HOURS * EC_POOL_PORTION(U)
                        P_CLASS_ASS_ECON_REV(2) = P_CLASS_ASS_ECON_REV(2) + TEMP_SELL_ENERGY*SEAS_HOURS * &
                        EC_POOL_PORTION(U) * SELL_PRICE/10.
                        TEMP_SELL_ENERGY = TEMP_SELL_ENERGY * EC_UTIL_PORTION(U)
                     ENDIF
                     P_CLASS_ASS_ECON_SELL(1) = P_CLASS_ASS_ECON_SELL(1) + TEMP_SELL_ENERGY*SEAS_HOURS
                     P_CLASS_ASS_ECON_REV(1) = P_CLASS_ASS_ECON_REV(1) + TEMP_SELL_ENERGY * SEAS_HOURS *SELL_PRICE/10.
                  ENDIF ! END SELL POOLING SECTION
                  TRANSACTION_REVENUE = TRANSACTION_REVENUE + TEMP_SELL_ENERGY * SELL_PRICE
!
                  PERIOD_NON_DISPATCHING_REV = PERIOD_NON_DISPATCHING_REV + SEAS_HOURS*TEMP_SELL_ENERGY*(SRP_SEGMENT_COST(S) - &
                                 BLOCK_DISP_COST(U,ECBLK))/10.
!
                  SEGMENT_ENERGY_REV(S) = SEGMENT_ENERGY_REV(S) + TEMP_SELL_ENERGY * SELL_PRICE
                  SEGMENT_SELLS(S) = SEGMENT_SELLS(S) + 1.
!
!                 SELL HEAT RATE CALCULATIONS
!
! HEAT MODIFIED 10/30/92 MSG NOTE LOOP ADDED AFTER ECONOMY INTERCHANGE
! TO SET THE LEFT AND RIGHT FOR HEAT CALCULATIONS TO THE CORRECT VALUES
! 
                  L_TEMP_HEAT(S) = MAX(MIN(R_TEMP_HEAT(S-1),TOP_PROB),L_TEMP_HEAT(S))
                  R_TEMP_HEAT(S) = MAX(R_TEMP_HEAT(S),MIN(L_TEMP_HEAT(S),TOP_PROB))

   50             S = S + 1
                  IF(S < NUMBER_OF_SEGMENTS) GOTO 40 ! 1/13/95. GAT.
               ENDIF ! RR_COST > TRANS_COST .AND. TIE_CAP < EXPORT_CAP
            ELSE ! RR == MARGINAL UNIT COST
!
!              NO TRANSACTION TAKE PLACE
!
            ENDIF
!
            MONTHLY_ECONOMY_BOUGHT = MONTHLY_ECONOMY_BOUGHT + TRANSACTION_ENERGY_BOUGHT
            MONTHLY_ECONOMY_COST = MONTHLY_ECONOMY_COST + TRANSACTION_COST
            MONTHLY_ECONOMY_SOLD = MONTHLY_ECONOMY_SOLD + TRANSACTION_ENERGY_SOLD
            MONTHLY_ECONOMY_REVENUE = MONTHLY_ECONOMY_REVENUE + TRANSACTION_REVENUE
!
            IF(ECONOMY_REPORT_ACTIVE .AND. .NOT. TESTING_PLAN .AND. ABS(TOP_PROB-BOTTOM_PROB) > .00000001) THEN
               KCOST_UNIT = UNIT(KCOST)
               KCOST_BLKNO = MAX(int(1,2),BLKNO(KCOST))
               IF(RR_COST < BLOCK_DISP_COST(KCOST_UNIT,KCOST_BLKNO)) THEN
                  LAST_TRANSACTION = S + 1
               ELSEIF(RR_COST > BLOCK_DISP_COST(KCOST_UNIT,KCOST_BLKNO)) THEN
                  LAST_TRANSACTION = S - 1
               ELSE 
                  LAST_TRANSACTION = S
               ENDIF
               LAST_TRANS_UNIT = UNIT(LAST_TRANSACTION)
               LAST_TRANS_BLKNO = MAX(int(1,2),BLKNO(LAST_TRANSACTION))
!               
               IF(KCOST_BLKNO > 2 .OR. KCOST_UNIT > NUNITS .OR. KCOST_UNIT < 1 .OR. LAST_TRANS_UNIT > NUNITS .OR. &
                          LAST_TRANS_BLKNO > 2 .OR. LAST_TRANS_UNIT < 1) THEN
                  WRITE(4,*)"*** STOP at line 624 NEW_ECONOMY_INTERCHANGE"
                  er_message='See WARNING MESSAGES -ECONINTR.FOR-5'
                  call end_program(er_message)
               ENDIF
!                  
               IF(TRANSACTION_ENERGY_BOUGHT > 0.) THEN
                  TRANSACTION_AVE_BUY = .1*TRANSACTION_COST/TRANSACTION_ENERGY_BOUGHT
               ELSE
                  TRANSACTION_AVE_BUY = 0.
               ENDIF
               IF(TRANSACTION_ENERGY_SOLD > 0.) THEN
                  TRANSACTION_AVE_SELL = .1*TRANSACTION_REVENUE/TRANSACTION_ENERGY_SOLD
               ELSE
                  TRANSACTION_AVE_SELL = 0.
               ENDIF
               IF(IMPORT_CAP == 0.) THEN
                  TIE_PERCENT = 0.
               ELSE
                  TIE_PERCENT = 100.*(TIE_CAP/IMPORT_CAP)
               ENDIF
               WRITE(ECONOMY_INTER_NO,REC=ECONOMY_INTER_REC) &
                    PRT_ENDPOINT(), &
                    FLOAT(YEAR+BASE_YEAR), &
                    CL_MONTH_NAME(PERIOD_COUNTER), &
                    FLOAT(TRANSACTION_POINT), &
                    (TOP_PROB-BOTTOM_PROB)*SEAS_HOURS, &
                    .1*RR_COST, &
                    .1*BLOCK_DISP_COST(KCOST_UNIT,KCOST_BLKNO), &
                    .1*BLOCK_DISP_COST(LAST_TRANS_UNIT,LAST_TRANS_BLKNO), &
                    TRANSACTION_ENERGY_BOUGHT*SEAS_HOURS, &
                    .0001*TRANSACTION_COST*SEAS_HOURS, &
                    TRANSACTION_AVE_BUY, &
                    TRANSACTION_ENERGY_SOLD*SEAS_HOURS, &
                    .0001*TRANSACTION_REVENUE*SEAS_HOURS, &
                    TRANSACTION_AVE_SELL, &
                    TIE_CAP, &
                    TIE_PERCENT 
               ECONOMY_INTER_REC = ECONOMY_INTER_REC + 1
               TRANSACTION_POINT = TRANSACTION_POINT + 1
            ENDIF
!     
            S_MAX = MAX(int(S,2),S_MAX)
            IF(YR .EQ. 1  .AND. PERIOD_COUNTER .EQ. 1) THEN
               IF(ILAST .NE. ICOST) ILAST = ICOST
            ENDIF
            IF(TOP_PROB .EQ. LAST_PROB .AND. BOTTOM_PROB .EQ. RR_PROB) THEN
               ICOST = ICOST + 1
            ELSEIF(TOP_PROB .EQ. LAST_PROB .AND. BOTTOM_PROB .EQ. LAST_RIGHT) THEN
               INCREMENT_KCOST = .TRUE.
            ELSEIF(TOP_PROB .EQ. LAST_RIGHT .AND. BOTTOM_PROB .EQ. RIGHT(K)) THEN
               INCREMENT_KCOST = .TRUE.
            ELSEIF(TOP_PROB .EQ. LAST_RIGHT .AND. BOTTOM_PROB .EQ. LAST_PROB) THEN
               ICOST = ICOST + 1
            ENDIF
            IF(TOP_PROB .EQ. LAST_PROB) THEN
               LAST_PROB = RR_PROB
               I = I + 1
               IF(EXTERNAL_RR) THEN
                  RR_PROB = 1. - FLOAT(I+1)/SEAS_HOURS
               ELSE
                  RR_PROB = OBS_AT_LOAD_POINT(I+1)
               ENDIF
            ENDIF
            IF(TOP_PROB .EQ. LAST_RIGHT) THEN
               LAST_RIGHT = RIGHT(K)
               K = NEXT_K
!            
!              TAKE CARE OF THE NON-DECREASING RIGHT'S
!
               DO WHILE(NEXT_K <= NUMBER_OF_SEGMENTS .AND. RIGHT(NEXT_K) >= RIGHT(K))
                  NEXT_K = NEXT_K + 1
               ENDDO
            ENDIF
            IF(INCREMENT_KCOST) THEN
               INCREMENT_KCOST = .FALSE.
               KCOST = K
               RIGHT_RIGHT = RIGHT(K) - RIGHT(NEXT_K)
            ENDIF
            S = KCOST
            FIRST_UNIT_PASS = .TRUE.
         ENDDO
!
! ADDED 10/30/92 MSG
!
      DO S = 1, NUMBER_OF_SEGMENTS
         U = UNIT(S)
         LEFT_HEAT(U) = L_TEMP_HEAT(S)
         RIGHT_HEAT(U) = R_TEMP_HEAT(S)
!
! ANNUAL UNIT TRANSACTIONS
!
         ECO_SALES_REV_FROM(U) = ECO_SALES_REV_FROM(U) + SEGMENT_ENERGY_REV(S)*SEAS_HOURS/10.
         ECO_SALES_ENRG_FROM(U) = ECO_SALES_ENRG_FROM(U) + SEGMENT_ENERGY_SOLD(S)*SEAS_HOURS
         ECO_PUCH_COST_FROM(U) = ECO_PUCH_COST_FROM(U) + SEGMENT_ENERGY_COST(S)*SEAS_HOURS/10.
         ECO_PUCH_ENRG_FROM(U) = ECO_PUCH_ENRG_FROM(U) + SEGMENT_ENERGY_BOUGHT(S)*SEAS_HOURS
!
         GRP = GENGRP(U)
         IF(GRP > 0 .AND. GRP <= MAX_REPORTING_GROUPS) THEN 
            GROUP_ECO_SALES(GRP) = GROUP_ECO_SALES(GRP) + SEGMENT_ENERGY_SOLD(S)*SEAS_HOURS
            GROUP_ECO_PURCHASES(GRP)=GROUP_ECO_PURCHASES(GRP) + SEGMENT_ENERGY_BOUGHT(S)*SEAS_HOURS
         ENDIF
!
      ENDDO
! 12/3/93. GAT. CHANGED MONTHLY COST AND REVENUE TO REFLECT $MM     
      MONTHLY_ECONOMY_BOUGHT = SEAS_HOURS*MONTHLY_ECONOMY_BOUGHT
      MONTHLY_ECONOMY_COST = SEAS_HOURS*MONTHLY_ECONOMY_COST/10.
      MONTHLY_ECONOMY_SOLD = SEAS_HOURS*MONTHLY_ECONOMY_SOLD
      MONTHLY_ECONOMY_REVENUE = SEAS_HOURS*MONTHLY_ECONOMY_REVENUE/10.
      ANNUAL_ECONOMY_BOUGHT = ANNUAL_ECONOMY_BOUGHT + MONTHLY_ECONOMY_BOUGHT
      ANNUAL_ECONOMY_COST = ANNUAL_ECONOMY_COST + MONTHLY_ECONOMY_COST
      ANNUAL_ECONOMY_SOLD = ANNUAL_ECONOMY_SOLD + MONTHLY_ECONOMY_SOLD
      ANNUAL_ECONOMY_REVENUE = ANNUAL_ECONOMY_REVENUE + MONTHLY_ECONOMY_REVENUE
!
      ANNUAL_NON_DISPATCHING_REV = ANNUAL_NON_DISPATCHING_REV + PERIOD_NON_DISPATCHING_REV/1000000.
!
!
      DEALLOCATE(BLOCK_DISP_COST)
!
      RETURN
      ENTRY GET_ECON_NON_DISPATCH_REV(R_SRP_NON_DISPATCH_REV)
         R_SRP_NON_DISPATCH_REV = ANNUAL_NON_DISPATCHING_REV
      RETURN
      ENTRY GET_GROUP_ECO_SALES_AND_PURCH(R_GROUP_ECO_SALES,R_GROUP_ECO_PURCHASES)
         DO  S = 1, MAX_REPORTING_GROUPS
            R_GROUP_ECO_SALES(S) = GROUP_ECO_SALES(S)/1000.
            R_GROUP_ECO_PURCHASES(S) = GROUP_ECO_PURCHASES(S)/1000.
         ENDDO
      RETURN
      END
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
      SUBROUTINE SRP_NEW_ECONOMY_INTERCHANGE(ENERGY,NBLOK2,BLKNO,ANNUAL_ECONOMY_BOUGHT,ANNUAL_ECONOMY_SOLD, &
               ANNUAL_ECONOMY_COST,ANNUAL_ECONOMY_REVENUE,UNIT,SEAS_HOURS,MAINTENANCE_RATE,UNITNM,PERIOD_COUNTER, &
               TRANSACTION_BUY_SPREAD,TRANSACTION_SELL_SPREAD,MONTHLY_ECONOMY_BOUGHT,MONTHLY_ECONOMY_COST, &
               MONTHLY_ECONOMY_SOLD,MONTHLY_ECONOMY_REVENUE,MWBLOK,EXCESS_ENERGY_SALES,TEMPEA,NUNITS,EAVAIL, & 
               SRP_SEGMENT_COST,ISEAS)
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
      use SpinDriftLib
      use prod_arrays_dimensions
      use eco
      USE SIZECOM

      LOGICAL (KIND=1) :: MARGIN_ACTIVE,ECON_MARGIN
      REAL (KIND=4) :: &
            ENERGY(2,MAX_CL_UNITS),ANNUAL_ECONOMY_BOUGHT,ANNUAL_ECONOMY_SOLD,ANNUAL_ECONOMY_COST,ANNUAL_ECONOMY_REVENUE, &
            MONTHLY_ECONOMY_BOUGHT,MONTHLY_ECONOMY_COST,MONTHLY_ECONOMY_SOLD,MONTHLY_ECONOMY_REVENUE,TRANSACTION_BUY_SPREAD, &
            TRANSACTION_SELL_SPREAD,MAINTENANCE_RATE(MAX_CL_UNITS),MWBLOK(MAX_DISPATCH_BLOCKS), &
            EXCESS_ENERGY_SALES(MAX_CL_UNITS),SEGMENT_ENERGY_UNUSED,TEMP_SELL_ENERGY,TEMPEA(2,MAX_CL_UNITS),SEAS_HOURS,GET_VAR, &
            EXCESS_ENERGY_SALES_PERCENT,EAVAIL(MAX_CL_UNITS),SRP_SEGMENT_COST(*),PERIOD_NON_DISPATCHING_REV, &
            ANNUAL_NON_DISPATCHING_REV=0.,R_SRP_NON_DISPATCH_REV
      INTEGER (KIND=2) :: NBLOK2,BLKNO(MAX_DISPATCH_BLOCKS),UNIT(MAX_DISPATCH_BLOCKS),PERIOD_COUNTER,U,ECBLK,SEG_NO,NUNITS,ISEAS
      CHARACTER (LEN=20) :: UNITNM(MAX_CL_UNITS)
!      
      REAL :: BLOCK_DISP_COST(:,:)
      ALLOCATABLE :: BLOCK_DISP_COST
!      
! END OF DATA DECLARATIONS      
!
      ALLOCATE(BLOCK_DISP_COST(NUNITS,2)) ! DEALLOCATED AT BOTTOM
      CALL GET_BLOCK_DISP_COST(UNIT,BLKNO,BLOCK_DISP_COST,NBLOK2)
!      
         MONTHLY_ECONOMY_COST = 0.
         MONTHLY_ECONOMY_REVENUE = 0.
         MONTHLY_ECONOMY_BOUGHT = 0.
         MONTHLY_ECONOMY_SOLD = 0.
!
         IF(ISEAS == 1) THEN
            ANNUAL_NON_DISPATCHING_REV = 0.
         ENDIF
         PERIOD_NON_DISPATCHING_REV = 0.
!
         MARGIN_ACTIVE = ECON_MARGIN()
!         
         DO SEG_NO = 1, NBLOK2
            U = UNIT(SEG_NO)
            IF(EXCESS_ENERGY_SALES(U) == 0.) THEN
               CYCLE
            ELSEIF(EXCESS_ENERGY_SALES(U) < 0.) THEN
               EXCESS_ENERGY_SALES_PERCENT = GET_VAR(EXCESS_ENERGY_SALES(U),PERIOD_COUNTER,UNITNM(U))
            ELSE
               EXCESS_ENERGY_SALES_PERCENT = EXCESS_ENERGY_SALES(U)
            ENDIF
            ECBLK = MAX(int(1,2),BLKNO(SEG_NO))
            SEGMENT_ENERGY_UNUSED = (MWBLOK(SEG_NO) * (1. - MAINTENANCE_RATE(U)) - ENERGY(ECBLK,U)) * EAVAIL(U)
            IF(SEGMENT_ENERGY_UNUSED == 0. .OR. TEMPEA(ECBLK,U) == 0.) CYCLE
            TEMP_SELL_ENERGY = MIN(SEGMENT_ENERGY_UNUSED, MAX(0.,SEGMENT_ENERGY_UNUSED * EXCESS_ENERGY_SALES_PERCENT/100.))
!     
            ENERGY(ECBLK,U) = ENERGY(ECBLK,U) + TEMP_SELL_ENERGY / TEMPEA(ECBLK,U)
            MONTHLY_ECONOMY_SOLD = MONTHLY_ECONOMY_SOLD+TEMP_SELL_ENERGY
            ECO_SALES_ENRG_FROM(U) = ECO_SALES_ENRG_FROM(U) + TEMP_SELL_ENERGY*SEAS_HOURS
!
            IF(.NOT. MARGIN_ACTIVE) THEN
               MONTHLY_ECONOMY_REVENUE = MONTHLY_ECONOMY_REVENUE + TEMP_SELL_ENERGY*(BLOCK_DISP_COST(U,ECBLK) + &
                                           10.*TRANSACTION_SELL_SPREAD)
               ECO_SALES_REV_FROM(U) = ECO_SALES_REV_FROM(U) + SEAS_HOURS*TEMP_SELL_ENERGY * (BLOCK_DISP_COST(U,ECBLK)/10.+ &
                                                TRANSACTION_SELL_SPREAD)
            ELSE
               MONTHLY_ECONOMY_REVENUE = MONTHLY_ECONOMY_REVENUE + TEMP_SELL_ENERGY*(SRP_SEGMENT_COST(SEG_NO) + &
                                            10.*TRANSACTION_SELL_SPREAD)
               ECO_SALES_REV_FROM(U) = ECO_SALES_REV_FROM(U) + SEAS_HOURS*TEMP_SELL_ENERGY*(SRP_SEGMENT_COST(SEG_NO)/10.+ &
                                                TRANSACTION_SELL_SPREAD)
            ENDIF
            PERIOD_NON_DISPATCHING_REV = PERIOD_NON_DISPATCHING_REV + SEAS_HOURS*TEMP_SELL_ENERGY*(SRP_SEGMENT_COST(SEG_NO) -  &
                                                BLOCK_DISP_COST(U,ECBLK))/10.

         ENDDO
         
         MONTHLY_ECONOMY_BOUGHT = SEAS_HOURS*MONTHLY_ECONOMY_BOUGHT
         MONTHLY_ECONOMY_COST = SEAS_HOURS*MONTHLY_ECONOMY_COST/10.
         MONTHLY_ECONOMY_SOLD = SEAS_HOURS*MONTHLY_ECONOMY_SOLD
         MONTHLY_ECONOMY_REVENUE = SEAS_HOURS*MONTHLY_ECONOMY_REVENUE/10.
         
         ANNUAL_ECONOMY_BOUGHT = ANNUAL_ECONOMY_BOUGHT + MONTHLY_ECONOMY_BOUGHT
         ANNUAL_ECONOMY_COST = ANNUAL_ECONOMY_COST + MONTHLY_ECONOMY_COST
         ANNUAL_ECONOMY_SOLD = ANNUAL_ECONOMY_SOLD + MONTHLY_ECONOMY_SOLD
         ANNUAL_ECONOMY_REVENUE = ANNUAL_ECONOMY_REVENUE + MONTHLY_ECONOMY_REVENUE
         ANNUAL_NON_DISPATCHING_REV = ANNUAL_NON_DISPATCHING_REV + PERIOD_NON_DISPATCHING_REV/1000000.
!
      DEALLOCATE(BLOCK_DISP_COST)
!
      RETURN
      ENTRY GET_SRP_NON_DISPATCH_REV(R_SRP_NON_DISPATCH_REV)
         R_SRP_NON_DISPATCH_REV = ANNUAL_NON_DISPATCHING_REV
      RETURN
      END

